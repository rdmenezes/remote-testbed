# Process this file with autoconf to produce a configure script.

AC_PREREQ(2.59)

AC_INIT([remote-mci],[2.0.git],[remote-testbed@googlegroups.com])
AC_CONFIG_AUX_DIR([config])
AC_CONFIG_HEADER([config/config.h])
AC_CONFIG_SRCDIR([remote.h])

AM_INIT_AUTOMAKE()

#######################################################################
## Helper macros.
#######################################################################

AC_DEFUN([REMOTE_PARSE_ENABLEVAL],
[m4_toupper(CONFIG_$1)=yes;
if test "$enable_$1" = "no"; then
	m4_toupper(CONFIG_$1)=no;
fi
AC_SUBST(m4_toupper(CONFIG_$1))
])


#######################################################################
## Configure options.
#######################################################################

AC_ARG_ENABLE(mch,
AS_HELP_STRING([--disable-mch],[disable building of the mote control host module]))
REMOTE_PARSE_ENABLEVAL(mch)

AC_ARG_ENABLE(mcs,
AS_HELP_STRING([--disable-mcs],[disable building of the mote control server module]))
REMOTE_PARSE_ENABLEVAL(mcs)

#######################################################################
## Checks for programs.
#######################################################################

AC_PROG_CXX
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_RANLIB
AC_PROG_MAKE_SET
AC_CHECK_TOOL(AR, ar, :)

# Optional documentation tools
AC_CHECK_TOOL(DOXYGEN, doxygen, :)
AC_CHECK_TOOL(ASCIIDOC, asciidoc, :)
AC_CHECK_TOOL(XMLTO, xmlto, :)
AM_CONDITIONAL(CONFIG_HTML, test "x$ASCIIDOC" != "x:")
AM_CONDITIONAL(CONFIG_MAN, test "x$ASCIIDOC" != "x:" && test "x$XMLTO" != "x:")

#######################################################################
## Checks for libraries.
#######################################################################

AX_LIB_MYSQL
AX_LIB_MYSQLPP

#######################################################################
## Checks for header files.
#######################################################################

AC_HEADER_STDC
AC_HEADER_SYS_WAIT

AC_CHECK_HEADERS([arpa/inet.h])
AC_CHECK_HEADERS([fcntl.h])
AC_CHECK_HEADERS([netdb.h])
AC_CHECK_HEADERS([netinet/in.h])
AC_CHECK_HEADERS([stdlib.h])
AC_CHECK_HEADERS([sys/ioctl.h])
AC_CHECK_HEADERS([sys/socket.h])
AC_CHECK_HEADERS([sys/time.h])
AC_CHECK_HEADERS([syslog.h])
AC_CHECK_HEADERS([termios.h])
AC_CHECK_HEADERS([unistd.h])

#######################################################################
## Checks for typedefs, structures, and compiler characteristics.
#######################################################################

AC_TYPE_INT16_T
AC_TYPE_INT32_T
AC_TYPE_INT64_T

AC_TYPE_UINT8_T
AC_TYPE_UINT16_T
AC_TYPE_UINT32_T
AC_TYPE_UINT64_T

AC_TYPE_PID_T
AC_TYPE_SSIZE_T

AC_C_CONST
AC_C_INLINE

AC_PROG_GCC_TRADITIONAL

AC_HEADER_STDBOOL
AC_HEADER_TIME

#######################################################################
## Checks for library functions.
#######################################################################

AC_FUNC_FORK
AC_FUNC_VPRINTF

AC_CHECK_FUNCS([gethostbyaddr])
AC_CHECK_FUNCS([gethostbyname])
AC_CHECK_FUNCS([inet_ntoa])
AC_CHECK_FUNCS([memset])
AC_CHECK_FUNCS([mkfifo])
AC_CHECK_FUNCS([select])
AC_CHECK_FUNCS([socket])

AC_TYPE_SIGNAL

#######################################################################
## Check dependencies.
#######################################################################

AM_CONDITIONAL(CONFIG_MCH, test "x$CONFIG_MCH" = "xyes")

if test "x$CONFIG_MCS" = "xyes"; then
	if test "x$CONFIG_MYSQL" != "xyes" ||
	   test "x$CONFIG_MYSQLPP" != "xyes"; then
		if test "x$enable_mcs" = "xyes"; then
			AC_MSG_ERROR([MCS dependencies not found on the system])
		else
			CONFIG_MCS=no
		fi
	fi
fi
AM_CONDITIONAL(CONFIG_MCS, test "x$CONFIG_MCS" = "xyes")

#######################################################################
## Output files.
#######################################################################

AC_CONFIG_FILES([
		 Makefile
		 doc/Makefile
		 libutil/Makefile
		 mch/Makefile
		 mch/notify/Makefile
		 mcs/Makefile
		 protocols/Makefile
])
AC_OUTPUT

#######################################################################
## Output build summary.
#######################################################################

AC_MSG_RESULT([Found dependencies:])
AC_MSG_RESULT([  MySQL library ................... $CONFIG_MYSQL])
AC_MSG_RESULT([  MySQL++ library ................. $CONFIG_MYSQLPP])
AC_MSG_RESULT([Will build:])
AC_MSG_RESULT([  Mote control host (MCH) ......... $CONFIG_MCH])
AC_MSG_RESULT([  Mote control server (MCS) ....... $CONFIG_MCS])
